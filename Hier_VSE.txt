model{
  
  mu_theta ~ dnorm(0, 1)T(0,1)
  mu_decay ~ dnorm(0, 1)T(0,1)
  mu_alfa ~ dnorm(0,1)T(0,1)
  mu_phi ~ dnorm(0, 1)
  mu_C ~ dunif(0, 5)
  
  l_theta ~ dgamma(.01,.01)
  l_decay ~ dgamma(.01,.01)
  l_alfa ~ dgamma(.01,.01)
  l_phi ~ dgamma(.01,.01)
  l_C ~ dgamma(.01,.01)
  
  for (s in 1:nsubs){
  
    theta[s] ~ dnorm(mu_theta, l_theta)
    decay[s] ~ dnorm(mu_decay, l_decay)
    alfa[s] ~ dnorm(mu_alfa, l_alfa)
    phi[s] ~ dnorm(mu_phi, l_phi)
    C[s] ~ dnorm(mu_C, l_C)
  
    Exploit[s,1,1] <- 0
    Exploit[s,1,2] <- 0
    Exploit[s,1,3] <- 0
    Exploit[s,1,4] <- 0
    
    Explore[s,1,1] <- 0
    Explore[s,1,2] <- 0
    Explore[s,1,3] <- 0
    Explore[s,1,4] <- 0

  
    for (t in 2:ntrials_all[s]){
      
      v[s,t-1] <- r[s,t-1]^theta[s] - l[s,t-1]^theta[s]
      
      for (d in 1:4){
        
        Exploit[s,t,d] <- ifelse(x[s,t-1] == d, Exploit[s,t-1,d]*decay[s] + v[s,t-1], Exploit[s,t-1,d]*decay[s])
        
        Explore[s,t,d] <- ifelse(x[s,t-1] == d, 0, Explore[s,t-1, d]+ alfa[s]*(phi[s] - Explore[s,t-1, d]))
        
        exp_p[s,t,d] <- exp((Exploit[s,t,d] + Explore[s,t,d])*C[s])
      }
      
      for (d in 1:4){
        
        p[s,t,d] <- exp_p[s,t,d]/sum(exp_p[s,t,])
        
      }
      x[s,t] ~ dcat(p[s,t,])
    }
  }
}